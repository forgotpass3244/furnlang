
import Gender

class Animal {
    name: String
    age: int
    gender: int

    Animal(name: String?, age: int?, gender: int?) {
        self.name = name || 'unnamed'
        self.age = age || 0
        self.gender = gender || Gender.Male
    }

    fun makeNoise() => println('\{name} growls')

    fun __ToString() :: String => 'Animal'
}

class Pettable {
    fun pet() => println('I was pet')
}

class Dog as Animal, Pettable {
    Breed: String

    Dog(name: String?, age: int?, gender: int?, Breed: String?) {
        self.Animal(name, age, gender)
        self.Breed = Breed || 'Golden Retriever'
    }

    # override the make noise method
    fun makeNoise() => println('\{self.name} barks')

    fun __ToString() :: String
    => 'name: \{new String(self.name)}\nAge: \{self.age}\nBreed: \{Breed}\nGender: \{self.gender}'
}

fun Main() {
    pet: Animal = new Dog('coco', 4, Gender.Female, 'Yorkie')
    println(pet, '\n')
    pet.makeNoise()
    (pet as? Dog)?.pet()

    print('\n')

    pet2 := new Dog('Tricky', 9, Gender.Male, 'some breed') with {
        fun pet() => println('\{self.name} does not like being pet')
    }
    
    pet2.pet()
    println('\n', pet2, '\n')
}

